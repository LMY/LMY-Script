[script]
n0=#AutoDeop off
n1=on @autodeop:TEXT:*:#:if ($nick isop $chan) mode $chan -o $nick
n2=on @autodeop:OP:#:mode $chan -o $opnick
n3=#AutoDeop end
n4=
n5=#AutoKick off
n6=on @autokick:TEXT:*:#:kick $nick # %msg.kick
n7=#AutoKick end
n8=
n9=#AutoOp off
n10=on @autoop:JOIN:#:mode $chan +o $nick
n11=#AutoOp end
n12=
n13=
n14=#AutoGreet off
n15=on !*:JOIN:#:if (($nick != $me) && ($me isop $chan)) say %msg.welcome $+ , $nick
n16=#AutoGreet end
n17=
n18=#BipEngine on
n19=on *:TEXT:BIP!:?:splay Bip!.wav
n20=#BipEngine end
n21=
n22=#ImportantNotify off
n23=on vip:NOTIFY:{ echo $color(ctcp) -at $nick is online! | splay Online.wav }
n24=on vip:UNOTIFY:echo $color(ctcp) -at $nick is now offline.
n25=#ImportantNotify end
n26=
n27=#OpAutoTnx off
n28=on *:OP:#:{
n29=  if ($chan isin %ops) halt
n30=  else if ($opnick == $me) { msg $chan Tnx $nick =) | set %ops %ops $+ $chan }
n31=}
n32=on *:CONNECT:unset %ops
n33=on *:DEOP:#:if ($opnick == $me) set %ops $remove(%ops,$chan)
n34=on *:PART:#:if ($nick == $me) set %ops $remove(%ops,$chan)
n35=on *:KICK:#:if ($knick == $me) set %ops $remove(%ops,$chan)
n36=#OpAutoTnx end
n37=
n38=
n39=#BanProtection on
n40=on @*:BAN:#:{
n41=  if ($bnick == $me) goto bptrigger
n42=  else {
n43=    var %i 0
n44=    while (%i < 10) {
n45=      if ($banmask iswm $address($me,%i)) {
n46=        :bptrigger
n47=        mode $chan -o-b $nick $banmask
n48=        kick $chan $nick %msg.banprot
n49=        halt
n50=      }
n51=      inc %i
n52=    }
n53=  }
n54=}
n55=#BanProtection end
n56=
n57=#MassDeopProtection on
n58=on @!*:DEOP:#:{
n59=  if (%deop. [ $+ [ $nick ] ] != $null) inc %deop. [ $+ [ $nick ] ]
n60=  if (((%deop. [ $+ [ $nick ] ] == 2) && ($nick(#,0,o) < 4)) || ((%deop. [ $+ [ $nick ] ] == 3) && ($nick(#,0,o) < 5))) { massdeop | halt }
n61=  else { set %deop. [ $+ [ $nick ] ] 1 | .timer 1 10 unset %deop. [ $+ [ $nick ] ] }
n62=  if (%deop. [ $+ [ $nick ] ] > 2) kick # $nick %msg.msdeop
n63=}
n64=on *:START:unset %deop.*
n65=on @!*:KICK:#:{
n66=  if (%deop. [ $+ [ $nick ] ] != $null) inc %deop. [ $+ [ $nick ] ]
n67=  else { set %deop. [ $+ [ $nick ] ] 1 | .timer 1 10 unset %deop. [ $+ [ $nick ] ] }
n68=  if (%deop. [ $+ [ $nick ] ] > 2) kick # $nick %msg.msdeop
n69=}
n70=#MassDeopProtection end
n71=
n72=#MassOpProtection off
n73=on @!*:OP:#:if ($mode(0).op >= 3) kick # $nick %msg.msop
n74=#MassOpProtection end
n75=
n76=#SplitProtection off
n77=on @*!:SERVEROP:#:mode $chan -o $opnick
n78=#SplitProtection end
n79=
n80=
n81=#DefaultCtcps on
n82=;ctcp ^*:version:*:{ ctcpreply $nick $$1 LMY-Script V3.7rc2 | echo $color(ctcp) -at $nick $+ --> Ctcp: Version | haltdef }
n83=ctcp ^*:finger:*:{ ctcpreply $nick $$1 Sorry... | echo $color(ctcp) -at $nick $+ --> Ctcp: Finger | haltdef }
n84=;ctcp *:$(lb $+ $chr(%Status.Random) $+ *):*: $2- | haltdef
n85=;ctcp *:$(lb0 $+ *):*: .notice $nick $chr(%Status.Random) | haltdef
n86=ctcp *:lb*:*: .notice $nick Sure, and my grandmother is a vagon | echo 4 -at $nick ( $+ $fulladdress $+ ) attempts an $1- | echo 4 -st $nick ( $+ $fulladdress $+ ) attempts an $1- | haltdef
n87=#DefaultCtcps end
n88=
n89=#DefaultRaws on
n90=raw 401:*:echo $color(ctcp) -at $2 $+ : Channel or Nick Doesn't Exist. | set %flag.a $true | .timer 1 10 /unset %flag.a | halt
n91=raw 402:*:echo $color(ctcp) -st Server Doesn't Exist. | halt
n92=raw 403:*:echo $color(ctcp) -st $2 $+ : Invalid Channel | halt
n93=raw 404:*:echo $color(ctcp) -t $2 Cannot send to channel (moderated?) | halt
n94=raw 405:*:echo $color(ctcp) -at U'r on too many channel, can't join others. | halt
n95=raw 421:*:echo $color(ctcp) -st Unknow Command: " $+ $2 $+ " | halt
n96=raw 432:*:echo $color(ctcp) -at Nick " $+ $2 $+ " is not valid. | halt
n97=raw 433:*:echo $color(ctcp) -at Nick " $+ $2 $+ " is already in use. | halt
n98=raw 464:*:echo $color(ctcp) -st Incorrect Password. | halt
n99=raw 465:*:echo $color(ctcp) -st U'r banned on this server! | halt
n100=raw 467:*:echo $color(mode) -t $2 Channel key already setted. | halt
n101=raw 471:*:echo $color(mode) -at Can't Join $2 $+ , channel is Limited. | halt
n102=raw 473:*:echo $color(mode) -at Can't Join $2 $+ , channel  is Invite Only. | halt
n103=raw 474:*:echo $color(mode) -at Can't Join $2 $+ , banned. | halt
n104=raw 475:*:echo $color(mode) -at Can't Join $2 $+ , key required. | halt
n105=raw 481:*:echo $color(ctcp) -st Permission Denied: You'r not an Irc-OP. | halt
n106=raw 381:*:echo $color(ctcp) -st You'r now an Irc-Op. | halt
n107=raw 482:*:echo $color(ctcp) -t $2 Permission Denied: You'r not a Channel Op | halt
n108=#DefaultRaws end
n109=
n110=#WhoisRaw on
n111=raw 311:*:{
n112=  echo $color(whois) $iif(%Status.WhoisWin, -a, -s) 
n113=  echo $color(whois) $iif(%Status.WhoisWin, -a, -s) class $+ $color(quit) $2  $+ $color(whois) $+ $chr(123)
n114=  echo $color(info2) $iif(%Status.WhoisWin, -a, -s) Host = $+ $color(whois) $3 $+ @ $+ $4 $+ ;
n115=  echo $color(info2) $iif(%Status.WhoisWin, -a, -s) RealName = $+ $color(whois) $6- $+ ;
n116=  unset %flag.a
n117=  halt
n118=}
n119=raw 312:*:echo $color(info2) $iif(%Status.WhoisWin, -a, -s) IrcServer = $+ $color(whois) $3  ( $+ $4- $+ ) $+ ; | halt
n120=raw 319:*:echo $color(info2) $iif(%Status.WhoisWin, -a, -s)  Channels = $+ $color(whois) $3- $+ ; | halt
n121=raw 313:*:echo $color(info2) $iif(%Status.WhoisWin, -a, -s) Status = $+ $color(ctcp) IRC-OP $+ $color(whois) $+ ; | halt
n122=raw 317:*:echo $color(info2) $iif(%Status.WhoisWin, -a, -s) Idle = $+ $color(whois) $3 $4 $+ ; | halt
n123=raw 301:*:echo $color(info2) $iif(%Status.WhoisWin, -a, -s) Away Message = $+ $color(whois) $3- $+ ; | halt
n124=raw 318:*:{
n125=  if (%flag.a != $null) unset %flag.a
n126=  else echo $color(whois) $iif(%Status.WhoisWin, -a, -s) };
n127=  echo $color(whois) $iif(%Status.WhoisWin, -a, -s) 
n128=  halt
n129=}
n130=#WhoisRaw end
